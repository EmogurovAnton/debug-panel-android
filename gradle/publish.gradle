apply plugin: "maven-publish"

apply from: rootProject.file("gradle/version.gradle")

def publishPropertiesFile = rootProject.file("gradle/publish.properties")
def publishProperties = new Properties()

if (publishPropertiesFile.canRead()) {
    publishProperties.load(new FileInputStream(publishPropertiesFile))
}

def libraryPropertiesFile = file("library.properties")
def libraryProperties = new Properties()

if (libraryPropertiesFile.canRead()) {
    libraryProperties.load(new FileInputStream(libraryPropertiesFile))
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}


afterEvaluate {
    publishing {
        repositories {
            maven {
                name = 'nexus'
                url libraryProperties["repository"]
                credentials {
                    username publishProperties["user"]
                    password publishProperties["key"]
                }
            }
        }
        publications {
            release(MavenPublication) {

                from components.release

                artifact androidSourcesJar

                groupId = libraryProperties["group_id"]
                artifactId = libraryProperties["lib_name"]
                version = getVersionNameFromProperties()
            }
        }

    }
}
